<!DOCTYPE html>
<html>
  <head>
    <title>CoffeeScript and SASS</title>
    <meta http-equiv="Content-Type" content="text/html; charset=UTF-8"/>

    <script type="text/javascript">
      RemarkConfig = {
        highlightStyle: "github",
        highlightInline: false
      }
    </script>

    <!-- <script src="https://github.com/downloads/gnab/remark/remark-0.3.0.min.js" type="text/javascript"></script>-->
    <script src="../remark/remark.min.js" type="text/javascript"></script>
    <script src="lib/highlight-coffee.js" type="text/javascript"></script>
    <script src="lib/jquery.js" type="text/javascript"></script>
    <script src="lib/coffee-script.js" type="text/javascript"></script>
    <script src="lib/eval-coffeescript.js" type="text/javascript"></script>

    <link href='http://fonts.googleapis.com/css?family=Yanone+Kaffeesatz:400,700,200' rel='stylesheet' type='text/css'>
    <link href='http://fonts.googleapis.com/css?family=Inconsolata' rel='stylesheet' type='text/css'>

    <style type="text/css" media="screen">
      html, body { font-family: 'Minion Pro', 'Palatino Linotype', 'Palatino', 'Georgia', serif; font-size: 24px; }
      h1, h2, h3, h4, h5, h6 { font-family: 'Yanone Kaffeesatz', sans-serif; font-weight: 400; }
      h1 {
        font-size: 4.5em;
        text-shadow: 0 -1px 0 #ff1810;
        color: #fff;
        background-color: #FF4943;
        padding: 20px;
        border: 1px solid #dc0700;
        -moz-border-radius:    15px;
        -webkit-border-radius: 15px;
        border-radius:         15px;
        -moz-box-shadow:    inset 0 1px 0 #ffaca9;
        -webkit-box-shadow: inset 0 1px 0 #ffaca9;
        -o-box-shadow:      inset 0 1px 0 #ffaca9;
        box-shadow:         inset 0 1px 0 #ffaca9;
      }
      h2 { font-size: 2.5em; }

      a { color: #41C8F0; text-decoration: none; }
      a:hover { color: #FF4943; text-decoration: none; }

      pre code { padding: 1em 2em; }

      p > code, li > code { padding: 1px 4px; border: 1px solid #CCC; background-color: #EEE; }

      #slideshow .slide .content code { font-family: 'Inconsololata', monospace; }
      code { position: relative; }
      code a.run { position: absolute; top: 10px; right: 10px; }
      code .function {color: #900;}

      .position { font-size: 0.8em; }

      .slide {}
      .slide > .red { background-color: #FF4943; color: #F0F2EB; }

      .red    { color: #FF4943; }
      .gray   { color: #787878; }
      .green  { color: #87A558; }
      .blue   { color: #41C8F0; }
      .yellow { color: #DBEC62; }

      .nice p { font-size: 2.5em; font-family: 'Yanone Kaffeesatz', sans-serif; font-weight: 400; color: #787878; }
    </style>
  </head>
  <body>
    <textarea id="source">

.center.middle

# CoffeeScript .yellow[&] SASS

---

.center.middle

Mitt navn er

## .blue[Kim Joar Bekkelund]

og jeg er konsulent i [BEKK](http://www.bekk.no).

---

## Agenda

1. Hva er *CoffeeScript*?
2. Hva er *SASS*?
3. Hvordan fungerer *the asset pipeline*?

---

## CoffeeScript

* Kompilerer ned til JavaScript.
* … men er ikke et supersett (som SASS er for CSS)
* Mindre kode enn JavaScript.
* "Ny" funksjonalitet på toppen av JavaScript.
* Unngår "the bad parts" av JavaScript, f.eks. globale variabler.
* Inspirert av Python og Ruby (dere vil kjenne igjen mye!)
* Default i Rails 3.1

---

.nice.middle.center

".blue[It's just JavaScript]"

---

.nice.middle.center

Men hva er egentlig JavaScript?

---

## JavaScript

Kjører i nettleseren. Vært der lenge, unngått i mange år. Renessanse. AJAX. Node.js.

---

## Men hva er problemet med JavaScript?

Et Ruby-eksempel:

    .ruby
    names = people.map do |person|
      person.name.capitalize!
    end

Men i JavaScript:

    .javascript
    var names = []
    for (var i = 0, l = people.length; i &lt; l; i++) {
      var person = people[i];
      var name = person.name.slice(0,1).toUpperCase() +
                 person.name.slice(1);
      names.push(name);
    }

WTF?

---

.middle.center

# .yellow[Intro til] CoffeeScript

---

## CoffeeScript ≈ Ruby = ♥

Mye er likt Ruby, for eksempel:

* Ingen semikolon
* Trenger ikke paranteser for å kalle metoder
* Kommentarer starter med `#`
* Interpolasjon → `#{coffeescripting}`
* Implisitt `return`

Men CoffeeScript har noen særegenheter fra Ruby:

* Signifikant whitespace
* Ikke-blokkerende IO (Mindre skummelt enn det høres ut)

---

## Variabler

    .coffee
    myVariable = "test"
    someNumber = 42
    aBoolean = true

Helt likt Ruby — men camelcased!

---

## Funksjoner

I CoffeeScript brukes `->` for å definere en funksjon:

    .coffee
    func = -> "bar"

Innholdet settes ofte på neste linje:

    .coffee
    func = ->
      "bar"

Med parameter:

    .coffee
    greet = (name) ->
      "hello #{name}!"

    alert greet("NITH")

---

## Funksjoner

Med default-variabler:

    .coffee
    greet = (name = "NITH") ->
      "hello #{name}!"

    alert greet()

Med *splats*:

    .coffee
    sum = (nums...) ->
      result = 0
      nums.forEach (n) -> result += n
      result

    alert sum(1,2,3)
    nums = [1,2,3]
    alert sum nums... # I Ruby ville man bruke *nums

---

## Funksjoner

Et velkjent Ruby-idiom er:

    .ruby
    def add_connection(host, options)
      port = options[:port]
      puts port
    end

    add_connection "localhost", :port => 2202
    add_connection "localhost", port: 2202 # Ruby 1.9

I CoffeeScript kan vi gjøre det samme:

    .coffee
    add_connection = (host, options) ->
      port = options.port
      alert port

    add_connection "localhost", port: 2202

---

## Funksjoner

Paranteser er valgfritt:

    .coffee
    sum = (a,b) -> a + b

    alert sum(5,4)
    alert sum 5,4

… men kun når en funksjon tar inn variabler:

    .coffee
    connection.close() # kaller funksjonen close
    connection.close   # returnerer funksjonen

---

## Førsteklasses funksjoner

I JavaScript kan man sende rundt funksjoner.

Altså kan en funksjon knyttes til en variabel, være input til en funksjon, returneres fra en funksjon, osv.

    .coffee
    close = (stuff) ->
      "closing #{stuff}"

    test = close

    alert test("db")

---

## Anonyme funksjoner

Det som egentlig har skjedd så langt er at vi har knyttet en variabel til en *anonym metode*.

Her knytter vi f.eks. variabelen `sum` til en funksjon som adderer to input-parametre.

    .coffee
    sum = (a,b) -> a + b

Selve funksjonen er altså kun:

    .coffee
    (a,b) -> a + b

---

## Deklarere objekter

Some å lage en hash i Ruby:

    .coffee
    father = {
      name: 'John',
      daughter: {
        name: 'Jill'
      },
      son: {
        name: 'Jack'
      }
    }

    alert father.son.name

---

## Deklarere objekter

La oss fjerne støyen:

    .coffee
    father =
      name: 'John'
      daughter:
        name: 'Jill'
      son:
        name: 'Jack'

    alert father.son.name

---

## Pattern matching

    .coffee
    list = ["Ruby", "Python", "JavaScript"]
    [head, tail...] = list
    alert head

Eller:

    .coffee
    list = ["Ruby", "Python", "JavaScript"]
    [head, rest..., bottom] = list
    alert bottom

Den er smart:

    .coffee
    list = ["Ruby", "JavaScript"]
    [head, rest..., bottom] = list
    alert head
    alert rest
    alert bottom

---

## Pattern matching på objekter

    .coffee
    me =
      name: "Kim Joar"
      company: "BEKK"

    { name, company } = me

    alert name
    alert company

---

## Ranges

Fungerer som i Ruby:

    .coffee
    alert [1..5]

Kan også brukes som i Ruby:

    .coffee
    users = ["Kim Joar", "Olav", "Sveinung", "Vetle"]
    alert users[0..2]
    alert users[1..]

Og det fungerer også på strenger:

    .coffee
    alert "test"[0..1]

---

## Iterere

Over arrays og ranges:

    .coffee
    nums = [1..10]

    for number in nums
      doStuffWith number

Med sjekk:

    .coffee
    nums = [1..10]

    for number in nums when number > 6
      doStuffWith number

---

## Iterere over objekter

(Legg merke til `of`, istedenfor `in`.)

    .coffee
    me = { name: "Kim Joar", company: "BEKK" }

    for key, value of me
      alert "#{key} = #{value}"

Også her kan vi gjøre en sjekk:

    .coffee
    me =
      name: "Kim Joar"
      company: "BEKK"
      statement: -> "hello world!"

    for key, value of me when typeof value is 'function'
      alert value()

---

## Sjekk at ting er tilstede eller satt

For å sjekke at et element er tilstede, kan man bruke `in`:

    .coffee
    frukter = ['eple', 'pære', 'banan']
    alert 'eple' in frukter

For å sjekke at en nøkkel er tilstede, kan man bruke `of`:

    .coffee
    me =
      name: "Kim Joar"
      company: "BEKK"

    alert 'name' of me
    alert 'statement' of me

---

## List Comprehensions

    .coffee
    doubles = (n * 2 for n in [1..5])
    alert doubles

Vi kan legge til et filter:

    .coffee
    large = (n for n in [1..5] when n > 2)
    alert large

Det sentrale med list comprehensions er at alle loop-er i CoffeeScript
returnerer en verdi, som er en array bestående av resultatet for hver
gjennomkjøring.

---

## if osv

Ganske likt Ruby:

    .coffee
    if person.name == "Kim Joar"
      alert "Yeah!"
    else
      alert "Bu!"

Det finnes en kort form:

    .coffee
    if ready() then attack() else wait()

Vi har også postfix-notasjon, som i Ruby:

    .coffee
    alert "great" if person.name == "Kim Joar"

---

## unless

Vi har unless også i CoffeeScript:

    .coffee
    alert "bu!" unless person.name == "Kim Joar"

## ?

Er en operator i CoffeeScript, ikke en del av funksjonsnavnet som i Ruby.

    .coffee
    practice() unless good_enough?

    c ?= d # er det samme som c = d unless c?

---

Tilbake til JavaScript-problemet:

    .javascript
    var names = []
    for (var i = 0, l = people.length; i &lt; l; i++) {
      var person = people[i];
      var name = person.name.slice(0,1).toUpperCase() +
                 person.name.slice(1);
      names.push(name);
    }

Løsning i CoffeeScript:

    .coffee
    names = for person in people
      capitalize person.name

---

.nice.middle

For å komme igang med å bruke CoffeeScript må vi forstå noen sentrale JavaScript-konsepter.

---

.nice.middle

JavaScript er .green[eventbasert]

---

## Eventer

JavaScript fungerer annerledes enn Ruby. I Ruby kan du skrive:

    .ruby
    data = get_data
    puts data

Men det samme ville ikke fungert i JavaScript.

Hvorfor?

---

## Eventer

Vi vet ikke om `open` er ferdig når det vi kaller `read`. JavaScript har ikke-blokkerende IO.

I JavaScript (og CoffeeScript) sier vi:

1. Start gjennomføringen av denne metoden
2. Gå videre
3. Eventuelt, når du er ferdig, kall en metode (ofte kalt en *callback*)

---

## Eventer

Istedenfor:

    .ruby
    data = get_data
    puts data

kunne vi i CoffeeScript ha skrevet:

    .coffee
    getData (data) ->
      alert data

Her kaller vi funksjonen `getData` med en anonym funksjon som input.

---

.nice.middle

.green[bind] kobler en funksjon til en event

---

## bind

Nettlesere har et sett med innebygde eventer, f.eks.:

* `click`
* `submit`
* `focus`
* `change`
* `mouseOver`

Vi kan dermed kalle en funksjon for eksempel når en bruker klikker på en link.

---

## Eksempel på bind



---

.nice.middle

JavaScript har .blue[lexical scoping] (WTF?!?)

---

## Lexical scoping

.gray[Psst, det er egentlig ganske simpelt og det er helt likt Ruby.]

    .coffee
    outer = 1
    changeNumbers = ->
      outer = 10

    changeNumbers()
    alert outer

Og hva skjer her?

    .coffee
    changeNumbers = ->
      outer = 10

    outer = 1
    changeNumbers()
    alert outer

---

## Lexical scoping

Grunnen til dette er at `outer` lever kun i metoden i det siste tilfellet. Det
kan vi se om vi gjør det følgende isteden:

    .coffee
    changeNumbers = ->
      outer = 10

    changeNumbers()
    alert outer

En variabel lever i et scope, som kan defineres på tre måter:

1. Enhver funksjon lager et scope.
2. En variabel lever i det ytterste scopet der den har blitt satt.
3. Utenfor sitt scope er variabelen usynlig.

---

.nice.middle

.blue[Context] a.k.a. .red[this] a.k.a. .green[@]

---

## this

I Ruby bruker man `self` for å vise til objektet. I CoffeeScript vil dere se
`this` og `@` mange plasser, men ikke bland de med `self` i Ruby.

I CoffeeScript bruker man `this`, *ikke* for å vise til objektet, men for å
vise til *"this context"*.

La oss ta et eksempel.

---

## this

    .coffee
    settNavn = (navn) -> @navn = navn
    settNavn("Kim Joar")
    alert this.navn # Don't do this at home!

    # Kobler funksjonen til et person-objekt
    person = {}
    person.settNavn = settNavn
    person.settNavn("Kim Joar")
    alert person.navn

På `person` blir `navn` tilgjengelig utenfor, mens utenom blir det ikke det. Da
er det satt på det *globale objektet*.

Dette er *veldig* praktisk når vi har førsteklasses funksjoner.

---

## Kall en funksjon i en gitt kontekst

Ved å bruke funksjonen `apply` kan man kalle en funksjon i en gitt kontekst:

    .coffee
    settNavn = (navn) -> @navn = navn

    person = {}

    settNavn.apply person, ["Kim Joar"]

    alert person.navn

---

## Scope vs kontekst

Scope avhenger av hvor funksjonen er definert.

Kontekst avhenger av hvordan funksjonen blir kalt.

---

## Bind funksjonen (this == this)

Når du ønsker at en funksjon kjører i den nåværende konteksten, uansett hvordan den er kalt.

    .coffee
    callback = (til) -> @mottakere.push(til)

Dersom denne metoden brukes som callback, vil `mottakere` sjekkes i det
objektet som *kaller* funksjonen, ikke der funksjonen er definert. Dette er
stress å fikse i JavaScript, men ikke i CoffeeScript. Løsningen:

    .coffee
    callback = (til) => @mottakere.push(til) # = istedenfor -

Et enkelt tegn. CoffeeScript ftw!

---

## Klasser

Som i Ruby brukes `class`. En klassen kan ha en `constructor` som kjøres når
man instansierer et objekt.

    .coffee
    class Person
      constructor: (name) ->
        @name = name

Legg merke til bruk av `:` istedenfor `=` som vi har brukt så langt. En klasse
er egentlig kun et objekt, som vi kan tenke på som en litt spesiell Ruby-hash.
Og til hver nøkkel knytter vi en anonym funksjon.

---

## Kode tilgjengelig på tvers av filer

For å ikke innføre mye global kode, er default i CoffeeScript å holde all kode
privat innenfor hver `.coffee`-fil. Dere vil fort få behov for å dele kode mellom filer.

Den enkleste måten å dele kode, er å knytte en funksjon eller klasse til det
*global objektet*, som i nettleseren er `window`:

    .coffee
    # fil1.coffee
    class Person
      constructor: (@name) ->

    window.Person = Person

    # fil2.coffee
    alert Person   # Man trenger ikke skrive window.Person
    alert window.Person is Person

Legg så lite kode som mulig i det globale objektet!

---

## Komme igang med CoffeeScript

Try CoffeeScript, kommandolinje, Rails 3.1!

---

.nice.middle.center

Les [CoffeeScript: Accelerated JavaScript Development](http://pragprog.com/book/tbcoffee/coffeescript)

---

.middle.center

Et av problemene med CoffeeScript er at man *burde* lære JavaScript.

(Debugging kan være vanskelig.)

---

.nice.middle.center

Les [JavaScript: The Good Parts](test)!

---

.middle.center

## Spørsmål?

Presentasjonen er laget med [remark](http://gnab.github.com/remark/).

    </textarea>
    <div id="slideshow"></div>
    <script type="text/javascript">
      var remark = this.remark;
      $(document).ready(function() {
        remark.events.addListener("slidein", function(slide, index) {
          loadConsolesOn("code.coffee:contains('alert')");
          loadConsolesOn("code.coffee:contains('console')");
        });
      });
    </script>
  </body>
</html>
